# CMake buildfile for the Firtree cpu runtime. 
#
# (C) Copyright 2009 Rich Wareham <richwareham@gmail.com>
# See LICENSE file for distribution rights.

FIND_PACKAGE(LLVM)
FIND_PACKAGE(PythonInterp)

SET(BIN2H "${CMAKE_SOURCE_DIR}/tools/bin2h.py")

IF(NOT PYTHONINTERP_FOUND)
  MESSAGE(SEND_ERROR "Could not find Python interpreter.")
ENDIF(NOT PYTHONINTERP_FOUND)

INCLUDE_DIRECTORIES("${CMAKE_SOURCE_DIR}")
INCLUDE_DIRECTORIES("${CMAKE_SOURCE_DIR}/firtree")
INCLUDE_DIRECTORIES("${CMAKE_SOURCE_DIR}/firtree/include")
INCLUDE_DIRECTORIES("${CMAKE_CURRENT_BINARY_DIR}")

ADD_CUSTOM_COMMAND(OUTPUT "${CMAKE_CURRENT_BINARY_DIR}/builtins.bc"
  COMMAND ${LLVM_AS_EXECUTABLE} ARGS builtins.il -f "-o=${CMAKE_CURRENT_BINARY_DIR}/builtins.bc"
  MAIN_DEPENDENCY builtins.il
  )
SET_SOURCE_FILES_PROPERTIES("${CMAKE_CURRENT_BINARY_DIR}/builtins.bc" PROPERTIES GENERATED 1)

ADD_CUSTOM_COMMAND(OUTPUT "${CMAKE_CURRENT_BINARY_DIR}/builtins.bc.opt"
  COMMAND ${LLVM_OPT_EXECUTABLE} ARGS "${CMAKE_CURRENT_BINARY_DIR}/builtins.bc" -f -std-compile-opts "-o=${CMAKE_CURRENT_BINARY_DIR}/builtins.bc.opt"
  MAIN_DEPENDENCY "${CMAKE_CURRENT_BINARY_DIR}/builtins.bc"
  )
SET_SOURCE_FILES_PROPERTIES("${CMAKE_CURRENT_BINARY_DIR}/builtins.bc.opt" PROPERTIES GENERATED 1)

ADD_CUSTOM_COMMAND(OUTPUT "${CMAKE_CURRENT_BINARY_DIR}/builtins_bc.h"
  COMMAND ${PYTHON_EXECUTABLE} ARGS ${BIN2H} builtins_bc "${CMAKE_CURRENT_BINARY_DIR}/builtins.bc.opt" "${CMAKE_CURRENT_BINARY_DIR}/builtins_bc.h"
  MAIN_DEPENDENCY "${CMAKE_CURRENT_BINARY_DIR}/builtins.bc.opt"
  DEPENDS ${BIN2H} 
  )
SET_SOURCE_FILES_PROPERTIES("${CMAKE_CURRENT_BINARY_DIR}/builtins_bc.h" PROPERTIES GENERATED 1)

ADD_DEFINITIONS("-DLLVM_HOST_TARGET=${LLVM_HOST_TARGET}")

SET_SOURCE_FILES_PROPERTIES(cpu-runtime.cpp PROPERTIES OBJECT_DEPENDS "${CMAKE_CURRENT_BINARY_DIR}/builtins_bc.h")

ADD_LIBRARY(cpu-runtime
  ../../include/firtree/cpu-runtime.h
  "${CMAKE_CURRENT_BINARY_DIR}/builtins_bc.h"
  cpu-runtime.cpp)

# vim:sw=2:ts=2:et
